BASE <http://reinfforce.iefc.net/data/>

PREFIX iter: <http://w3id.org/sparql-generate/iter/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 
PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX time: <http://www.w3.org/2006/time#>

PREFIX ifn: <https://datos.iepnb.es/def/sector-publico/medio-ambiente/ifn/>
PREFIX smo:   <http://crossforest.eu/measures/ontology/> 
PREFIX unit:  <http://crossforest.eu/measures/data/unit/>

PREFIX vfo: <http://virtualforests.gsic.uva.es/>
PREFIX rff: <http://reinfforce.iefc.net/data/>


GENERATE { 
	<arboretum/{?idArboretum}> a vfo:Arboretum ;
        vfo:hasArboretumProvenance <arbProvenance/{?idArbProv}> .
  
  <arbProvenance/{?idArbProv}> a vfo:ArboretumProvenance ;
        vfo:hasProvenance <provenance/{?upov_prov}> ;
        vfo:isInArboretum <arboretum/{?idArboretum}> ;
        vfo:hasTree <tree/{?idTree}> .

  <tree/{?idTree}> a ifn:Tree , <provenance/{?upov_prov}> ;
        vfo:isInArboretum <arboretum/{?idArboretum}> ;
        vfo:hasLastAnnualGrowthRateInCentimetersPerYear ?yearlyGrowthNum ;
        vfo:hasAnnualGrowthRate <annualGrowthRate/{?yearlyGrowth}> .
    
    <annualGrowthRate/{?yearlyGrowth}> 
        a vfo:AnnualGrowthRate , owl:NamedIndividual ;
        smo:hasUnit unit:CentimetersPerYear ;
        smo:hasValue ?yearlyGrowthNum ;
        vfo:hasIntervalTime  <intervalTime/{?years}> .

    <intervalTime/{?years}> 
        a       vfo:IntervalTime , time:ProperInterval ;
        time:hasXSDDuration ?duration ;
        time:hasBeginning <year/{?begin}> ;
        time:hasEnd <year/{?end}> .

      <year/{?begin}> a time:Instant ;
                time:inXSDgYear ?year_begin  .

      <year/{?end}> a time:Instant ;
                time:inXSDgYear ?year_end .
}

SOURCE <urn:source> AS ?source
ITERATOR iter:CSV(?source, true, "\"", ";", "\n", "ID_Site", "ID_PROVENANCE", "Species", "Provenance",
"YearlyGrowth", "ID UPOV Prov", "ID Tree", "Years", "Begin" , "End") AS ?idArboretum ?idProv ?spReinf_id ?prov ?yearlyGrowth
?upov_prov ?tree ?years ?begin ?end
WHERE {
  BIND("{?idArboretum}-{?upov_prov}" as ?idArbProv )
  BIND("{?idArboretum}-{?upov_prov}-{?tree}" as ?idTree )
  BIND(xsd:decimal(?yearlyGrowth) as ?yearlyGrowthNum)
  BIND(xsd:gYear(?begin) as ?year_begin)
  BIND(xsd:gYear(?end) as ?year_end)
  BIND(xsd:duration(CONCAT("P" , str(YEAR(?year_end) - YEAR(?year_begin)), "Y")) as ?duration)
}